require conf/distro/poky.conf

# Distro name and version
DISTRO := "cetibox-base"
DISTRO_NAME := "CETiBox H3 Base Level"
DISTRO_VERSION := "V02.00.00"
DISTRO_CODENAME := "Crucial Coffea"
MAINTAINER := "info@cetitec.com"

# SDK config
SDK_TITLE = "${DISTRO_NAME} SDK"
SDK_VERSION = "${DISTRO_VERSION}"
SDK_NAME = "cetibox-x3-${TCLIBC}-${SDK_ARCH}-${IMAGE_BASENAME}-${TUNE_PKGARCH}"
SDKPATH = "/opt/cetitec/cetibox_x3/${DISTRO_VERSION}"
# add the static lib to SDK toolchain
SDKIMAGE_FEATURES = "dev-pkgs dbg-pkgs staticdev-pkgs"
# add kernel source
TOOLCHAIN_TARGET_TASK_append = " kernel-devsrc linux-cetibox-kernel-headers"

# Package config
# Use dropbear ssh
TASK_BASIC_SSHDAEMON := "dropbear"
# Set distro features
DISTRO_FEATURES = "alsa ext2 ipv6 argp largefile usbgadget usbhost wifi xattr pci systemd"
VIRTUAL-RUNTIME_init_manager = "systemd"

# lttng is incompatible with our kernel, don't build
RDEPENDS_packagegroup-core-tools-profile_remove = " \
	lttng-modules \
	lttng-tools \
	lttng-ust \
"

# Don't build/install ltp
RDEPENDS_packagegroup-core-tools-testapps_remove = "ltp"

# Don't install connman, wpa_supplicant, ofono
RDEPENDS_packagegroup-core-tools-testapps_remove = " \
	connman-tools \
	connman-tests \
	connman-client \
	ofono-tests \
"

# We are systemd-only, do not install any sysv init scripts
# Don't need pulseaudio for now
DISTRO_FEATURES_BACKFILL_CONSIDERED = "sysvinit pulseaudio"

# Package kernel device trees for h3
KERNEL_DEVICETREE = " \
	renesas/r8a7795-cetibox-h3ulcb-gweb-v2.dtb \
	renesas/r8a7795-cetibox-h3vc2-nxp.dtb \
	renesas/r8a7795-cetibox-h3vc2-fpga.dtb \
"

PACKAGECONFIG_remove_pn-alsa-plugins = "speexdsp"

# Due to python2/3 incompatibility with old perf (4.14) and new yocto (3.0)
PACKAGECONFIG_remove_pn-perf = "scripting"